plugins {
		id 'byteback.java-common-conventions'
		id 'byteback.java-integration-conventions'
		id 'application'
}

dependencies {
		implementation project(':byteback-annotations')
		implementation project(':byteback-frontend:boogie')
		implementation 'org.soot-oss:soot:4.1.0'
		implementation 'org.slf4j:slf4j-simple:1.6.1'
		implementation 'com.beust:jcommander:1.+'
		testImplementation 'org.mockito:mockito-core:3.+'
}

application {
		mainClass = 'byteback.core.Main'
}

def getDummyJars() {
		def jars = []

		project(':byteback-dummy').subprojects.each { dummy ->
				jars += dummy.shadowJar
		}

		return jars;
}

def copyIntegrationResources = tasks.register('copyIntegrationResources', Copy) {
		from getDummyJars();
		into 'src/integration/resources/compiled/'
}
integration.dependsOn copyIntegrationResources

def copySystemResources = tasks.register('copySystemResources', Copy) {
		from getDummyJars();
		into 'src/system/dummy/'
}

def system = tasks.register('system', Exec) {
		workingDir 'src/system/'
		commandLine 'lit', 'tests/'
		dependsOn installDist
		dependsOn copySystemResources
}
