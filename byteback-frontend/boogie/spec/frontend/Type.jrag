import java.util.Optional;

aspect Type {

  /**
   * Scope collections.
   */
  coll EntityTable<TypeDefinition> Program.typeDefinitions();

  TypeDeclaration contributes getTypeDefinition() to Program.typeDefinitions();

  /**
   * NTA definitions.
   */
  syn nta TypeDefinition TypeDeclaration.getTypeDefinition();

  eq TypeDeclaration.getTypeDefinition() {
    final DefinedType type = new DefinedType();
    final TypeDefinition typeDefinition = new TypeDefinition();
    typeDefinition.setDeclarator(getDeclarator());
    typeDefinition.setDefinedType(type);

    return typeDefinition;
  }

  eq TypeSynonymDeclaration.getTypeDefinition() {
    final TypeDefinition typeDefinition = super.getTypeDefinition();
    typeDefinition.setAliased(getAliased());

    return typeDefinition;
  }

  /**
   * Looking up type definitions.
   */
  syn lazy Optional<TypeDefinition> Accessor.type();

  eq Accessor.type() = lookupTypeDefinition(getName());

  /**
   * Lookup type definition in program.
   */
  syn lazy Optional<TypeDefinition> Program.lookupTypeDefinition(String name);

  eq Program.lookupTypeDefinition(String name) = typeDefinitions().getFirst(name);

  inh lazy Optional<TypeDefinition> Accessor.lookupTypeDefinition(String name);

  eq Program.getChild().lookupTypeDefinition(String name) = lookupTypeDefinition(name);

  /**
   * Link type definitions with concrete types.
   */
  syn nta DefinedType TypeDefinition.getType();

  eq TypeDefinition.getType() = new DefinedType();

  /**
   * Link concrete types with their accessors.
   */
  syn TypeAccess Type.makeTypeAccess();

  eq BooleanType.makeTypeAccess() = new BooleanTypeAccess();

  eq IntegerType.makeTypeAccess() = new IntegerTypeAccess();

  eq RealType.makeTypeAccess() = new RealTypeAccess();

  eq MapType.makeTypeAccess() = new MapTypeAccess();

  eq BitvectorType.makeTypeAccess() = new BitvectorTypeAccess();

  syn UnknownTypeAccess DefinedType.makeTypeAccess();

  eq DefinedType.makeTypeAccess() = new UnknownTypeAccess(makeAccessor(), new List<>());

  /**
   * Defined type attributes. 
   */
  inh Accessor DefinedType.makeAccessor();

  eq TypeDeclaration.getChild().makeAccessor() = new Accessor(getDeclarator().getName());

  inh lazy List<TypeParameter> DefinedType.getTypeParameterList();

  eq TypeDeclaration.getChild().getTypeParameterList() = getTypeParameterList();

  /**
   * Get String identifier from type access.
   */
  syn lazy String TypeAccess.getIdentifier();

  eq BooleanTypeAccess.getIdentifier() = "bool";

  eq IntegerTypeAccess.getIdentifier() = "int";

  eq RealTypeAccess.getIdentifier() = "real";

  eq UnknownTypeAccess.getIdentifier() = getAccessor().getName();

  eq MapTypeAccess.getIdentifier() { throw new UnsupportedOperationException(); }

  eq BitvectorTypeAccess.getIdentifier() { throw new UnsupportedOperationException(); }

}
