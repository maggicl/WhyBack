subprojects { test ->

		def core = project(':byteback-core')

		def bytebackRoot = core.installDist.destinationDir

		def system = tasks.register('system') {
				def sourceDirs = project.sourceSets.main.allSource.srcDirs
				def targetProperty = project.findProperty("TARGET")

				def execute = { target ->
						test.exec {
								environment 'BYTEBACK_ROOT', bytebackRoot
								environment 'TEST_JAR', project.shadowJar.archivePath
								commandLine 'lit', target
						}
				}

				doLast {
						sourceDirs.each { dir ->
								def target = targetProperty

								if (target == null) {
										if (dir.getName().equals("java") || dir.getName().equals("scala")) {
												execute(dir)
										}
								} else {
										println target
										execute(target)
								}
						}
				}

				dependsOn core.installDist
				dependsOn test.shadowJar
		}

		def benchmark = tasks.register('benchmark') {
				def script = "../benchmark.py"
				def jar = test.shadowJar.archivePath
				def sourceDirs = test.sourceSets.main.allSource.srcDirs

				doLast {
						sourceDirs.each { dir ->
								test.exec {
										def source = dir.getName()
										def benchmark = "./benchmark/$source"
										def output = "$benchmark/results.csv"
										def tmp = "$benchmark/tmp"
										environment 'BYTEBACK_ROOT', bytebackRoot
										project.mkdir(benchmark)
										project.mkdir(tmp)

										commandLine 'python', script, '--jar', jar,
												'--source', dir, '--output', output, '--temp', tmp,
												'--extension', source
								}
						}
				}

				dependsOn core.installDist
				dependsOn test.shadowJar
		}

}

