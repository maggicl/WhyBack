module Type
  use prelude.ptr.Ptr
  use prelude.ptr.Operators
  use ref.Ref
  use map.Map

  (* to be used for classes *)
  type class = abstract { }

  (* enumerates all non-primitive types *)
  type t = BoolArray |
           ByteArray |
           ShortArray |
           CharArray |
           IntArray |
           LongArray |
           FloatArray |
           DoubleArray |
           Class class |
           ArrayOf t

  (* the "root" class. This value is used for java.lang.Object prelude *)
  val constant root: class

  (* used when the type is unknown, matches object *)
  let constant unknown: t = Class root

  (* if true, t1 extends t2 *)
  val predicate (:>) (t1: t) (t2: t)

  clone export relations.PartialOrder with type t = t, predicate rel = (:>)

  axiom root_is_top: forall x:t. x :> Class root
  axiom array_covariance: forall x:t, x2:t. x :> x2 <-> ArrayOf x :> ArrayOf x2

  let predicate compatible (x: Ptr.t) (tmap: Ptr.t -> t) (typ: t) = is_null x || tmap x :> typ

  (* do not inline this as the expression to consume needs to be evaluated for its side effects *)
  let function void (to_consume: 'a) : unit = ()
end
